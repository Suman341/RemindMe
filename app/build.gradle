apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.reminder.remindme"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {

        debug {
            testCoverageEnabled !project.hasProperty('android.injected.invoked.from.ide')
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        androidTest.java.srcDirs += "src/test-common/java"
        test.java.srcDirs += "src/test-common/java"
    }
    lintOptions {
        lintConfig rootProject.file('lint.xml')
    }

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
}

dependencies { configuration ->
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    androidJetPackComponents(configuration, '2.2.0-beta01')
    androidXComponents(configuration)

    // Room CBDatabase and LiveData
    implementation "android.arch.lifecycle:runtime:1.1.1"
    implementation "android.arch.lifecycle:extensions:1.1.1"
    implementation "android.arch.lifecycle:common-java8:1.1.1"

    // Dagger V2
    implementation "com.google.dagger:dagger:2.19"
    implementation "com.google.dagger:dagger-android:2.19"
    implementation "com.google.dagger:dagger-android-support:2.19"

    // Log library
    implementation "com.jakewharton.timber:timber:4.5.1"

    // Dagger, Room, and LiveData annotationProcessor

    annotationProcessor "com.google.dagger:dagger-android-processor:2.19"
    annotationProcessor "com.google.dagger:dagger-compiler:2.19"

    annotationProcessor "android.arch.lifecycle:compiler:1.1.1"

    // Couchbase Lite android
    implementation "com.couchbase.lite:couchbase-lite-android:1.3.1"

    implementation "io.github.luizgrp.sectionedrecyclerviewadapter:sectionedrecyclerviewadapter:1.2.0"

    implementation "com.amulyakhare:com.amulyakhare.textdrawable:1.0.1"

    implementation 'com.bignerdranch.android:recyclerview-multiselect:+'

    testingComponents(configuration)
}


private static void androidJetPackComponents(configuration, lifecycleVersion) {
    configuration.implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"
    configuration.implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
    configuration.implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    configuration.implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"
}

private static void androidXComponents(configuration) {
    configuration.implementation 'com.google.android.material:material:1.2.1'
    configuration.implementation "androidx.browser:browser:1.2.0"
    configuration.implementation "androidx.fragment:fragment-ktx:1.1.0"
    configuration.implementation "androidx.activity:activity-ktx:1.1.0"
    configuration.implementation 'androidx.cardview:cardview:1.0.0'
    configuration.implementation "androidx.multidex:multidex:2.0.1"
    configuration.implementation 'androidx.appcompat:appcompat:1.1.0'
    configuration.implementation 'androidx.annotation:annotation:1.1.0'
    configuration.implementation "androidx.recyclerview:recyclerview:1.1.0"
    configuration.implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta3'
}

private static void testingComponents(configuration) {
    // JUnit 4 framework
    configuration.testImplementation 'junit:junit:4.13'

    configuration.androidTestImplementation 'androidx.test:core:1.1.0'

    configuration.debugImplementation 'androidx.fragment:fragment-testing:1.2.5'

    configuration.androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'

    // LiveData
    configuration.testImplementation 'com.jraska.livedata:testing:1.0.0'

    configuration.testImplementation "android.arch.core:core-testing:1.1.1"

    configuration.androidTestUtil 'androidx.test:orchestrator:1.1.1'

    // Robolectric environment
    configuration.testImplementation 'org.robolectric:robolectric:4.1'
    configuration.testImplementation "org.robolectric:shadows-multidex:4.1"

    // Mockito framework
    configuration.testImplementation 'org.mockito:mockito-core:2.24.5'
    configuration.testImplementation 'org.mockito:mockito-inline:2.24.5'
    configuration.testImplementation 'org.powermock:powermock-module-junit4:2.0.0'
    configuration.testImplementation 'org.powermock:powermock-api-mockito2:2.0.0'
    configuration.androidTestImplementation 'org.mockito:mockito-android:2.24.5'

    // Assertions
    configuration.androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    configuration.testImplementation "org.assertj:assertj-core:3.8.0"

    // AndroidJUnitRunner and JUnit Rules
    configuration.androidTestImplementation 'androidx.test:rules:1.3.0'
    configuration.androidTestImplementation 'androidx.test:runner:1.3.0'
}